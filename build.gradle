buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
	}
}

plugins {
	id "net.minecraftforge.gradle.forge" version "2.0.2"
	id "com.matthewprenger.cursegradle" version "1.0.8"
	id "se.bjurr.gitchangelog.git-changelog-gradle-plugin" version "1.32"
}

apply plugin: 'maven'


version = version_major + '.' + version_minor + '.' + version_patch + '.' + getBuildNumber()
if (project.hasProperty('buildQualifier')) {
	version = project.version + '-' + project.buildQualifier
}
group= "de.nedelosk.modularmachines" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "modularmachines_" + mcversion

ext.dirDest = './build/dist'

// java version
sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
	version = mcversion + "-" + forgeversion
	runDir = "run"
	mappings = mcp_mappings

	replace '@VERSION@', project.version
	replace '@BUILD_NUMBER@', getBuildNumber()
}

jar {
    manifest {
        attributes 'FMLAT': 'modularmachines_at.cfg'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allJava
	classifier = 'sources'
}

task apiJar(type: Jar) {
	from sourceSets.main.allSource
	from sourceSets.main.output
	include 'de/nedelosk/modularmachines/api/**'
	classifier = 'api'
}

artifacts {
	archives deobfJar
	archives sourcesJar
	archives apiJar
}

repositories {
	maven {
		url "http://dvs1.progwml6.com/files/maven"
	}
	maven {
		name = "ic2"
		url = "http://maven.ic2.player.to/"
	}
	maven {
		url 'http://maven.epoxide.xyz'
	}
}

dependencies {
	deobfCompile "net.darkhax.tesla:Tesla:${tesla_version}"
	deobfCompile "mezz.jei:jei_${mcversion}:${jei_version}:api"
	runtime "mezz.jei:jei_${mcversion}:${jei_version}"
	deobfCompile "net.industrial-craft:industrialcraft-2:${ic2_version}:api"
	
}

processResources {

	// this will ensure that this task is redone when the versions change.
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	// replace stuff in mcmod.info, nothing else
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		// replace version and mcversion
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}

	// copy everything else, thats not the mcmod.info
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
		exclude '**/*.md'
	}
	
	// Move access transformers to META-INF
	rename '(.+_at.cfg)', 'META-INF/$1'
}

def getBuildNumber() {
	return "$System.env.BUILD_NUMBER" != "null" ? "$System.env.BUILD_NUMBER" : "0"
}

/*task makeChangelog(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
	filePath = "changelog.html"
	untaggedName = "Current release ${project.version}"
	fromCommit = "422008f48869bdb14c60409ba20708b8a5ecd212"
	toRef =  "HEAD"
	templateContent = """
Alpha version, still buggy. Do not create a world you care about with this version, it can and will break things severely.<br/>
Please help the project by trying it out and <a href="https://github.com/Nedelosk/Modular-Machines/issues">reporting issues here.</a><br/>
{{#tags}}
  <h3>{{name}}</h3>
  <ul>
    {{#commits}}
    <li>{{{message}}} - {{authorName}}</li>
    {{/commits}}
  </ul>
{{/tags}}
"""
}

curseforge {
	apiKey = project.hasProperty('curseforge_apikey') ? project.curseforge_apikey : '0'
	project {
		id = 233073
		changelog = file('changelog.html')
		changelogType = 'html'
		releaseType = 'alpha'
		addArtifact sourcesJar
		addArtifact apiJar
		addArtifact deobfJar
	}
}

afterEvaluate {
	tasks.curseforge233073.dependsOn.add(makeChangelog)
}*/
